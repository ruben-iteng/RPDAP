#pragma experiment("TRAITS")
#pragma experiment("FOR_LOOP")
import ElectricPower
import ElectricLogic
import SWD
import JTAG
import UART_Base
import has_single_electric_reference_shared

from "parts/Megastar_ZX_IDC2_54_2_10PWZ/Megastar_ZX_IDC2_54_2_10PWZ.ato" import Megastar_ZX_IDC2_54_2_10PWZ_package
from "parts/XFCN_BH254VS_20P/XFCN_BH254VS_20P.ato" import XFCN_BH254VS_20P_package

module IDC_Placholder:
    """
    IDC placeholder for use in J-Link interface.
    """
    pin 1
    pin 2
    pin 3
    pin 4
    pin 5
    pin 6
    pin 7
    pin 8
    pin 9
    pin 10
    pin 11
    pin 12
    pin 13
    pin 14
    pin 15
    pin 16
    pin 17
    pin 18
    pin 19
    pin 20

module JLINKInterfaceTHHorizontal from IDC_Placholder:
    """
    2x10 pin TH horizontal IDC header with J-Link interface pinout.
    https://www.segger.com/products/debug-probes/j-link/technology/interface-description/
    """
    package = new Megastar_ZX_IDC2_54_2_10PWZ_package

module JLINKInterfaceSMDVertical from IDC_Placholder:
    """
    2x10 pin SMD vertical IDC header with J-Link interface pinout.
    https://www.segger.com/products/debug-probes/j-link/technology/interface-description/
    """
    package = new XFCN_BH254VS_20P_package

module JLINKInterface:
    """
    2x10 pin IDC header with J-Link interface pinout.
    https://www.segger.com/products/debug-probes/j-link/technology/interface-description/
    """

    # --- External interfaces ---
    swd = new SWD
    """SWD debug interface"""
    jtag = new JTAG
    """JTAG debug interface"""
    power_5v = new ElectricPower
    """Optional 5V power to the target"""
    power_logic_reference = new ElectricPower
    """Reference power for logic signals from target"""
    power_logic_reference.required = True
    ground_detect = new ElectricLogic
    """
    For target to detect the pressence of a debugger.
    Connected to GND on the official JLink header.
    """
    return_test_clock = new ElectricLogic
    """
    Return test clock. Jused in advanced JTAG configurations.
    """
    uart = new UART_Base

    # --- Components ---
    package = new IDC_Placholder

    # --- Connections ---
    # JTAG
    package.1 ~ jtag.vtref.hv
    package.3 ~ jtag.n_trst.line
    package.5 ~ jtag.tdi.line
    package.7 ~ jtag.tms.line
    package.9 ~ jtag.tck.line
    package.11 ~ return_test_clock.line
    package.13 ~ jtag.tdo.line
    package.15 ~ jtag.n_reset.line
    package.17 ~ jtag.dbgrq.line

    # target power
    package.19 ~ power_5v.hv

    # SWD
    package.7 ~ swd.dio.line
    package.9 ~ swd.clk.line
    package.13 ~ swd.swo.line
    package.15 ~ swd.reset.line

    # UART
    package.5 ~ uart.tx.line
    package.17 ~ uart.rx.line

    # GND
    package.4 ~ power_logic_reference.gnd
    package.6 ~ power_logic_reference.gnd
    package.8 ~ power_logic_reference.gnd
    package.10 ~ power_logic_reference.gnd
    package.12 ~ power_logic_reference.gnd
    # Pin 14-16 are on newer J-Link products used for firmware extension purposes.
    # They can be left open or connected to ground.
    # Pin 14 is connected to GND detect, can be externally connected to ground if not used.
    package.14 ~ ground_detect.line
    # package.16 ~ target_power.gnd
    # package.18 ~ target_power.gnd
    # package.20 ~ target_power.gnd

    # references
    for logic in [swd, jtag, uart, ground_detect, return_test_clock]:
        logic.reference_shim ~ power_logic_reference
    trait has_single_electric_reference_shared<gnd_only=True>

    # --- Parameters ---
    assert power_5v.voltage <= 5.0V
    assert power_logic_reference.voltage <= 5.0V
